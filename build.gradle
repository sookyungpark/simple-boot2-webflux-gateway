buildscript {
    ext {
        springBootVersion = '2.6.7'
        artifactRepoBase = "http://oss.jfrog.org/artifactory"
        baseVersion = "1.0.1"
        jacksonVersion = "2.13.2"
        kotlinVersion = '1.6.21'
        micrometerVersion = '1.1.3'
        serviceProjects = [ "coffeeshop-api" ]
    }

    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://repo.maven.apache.org/maven2" }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
        classpath 'de.undercouch:gradle-download-task:3.4.3'
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
    }

    ext["elasticsearch.version"] = '5.6.8'
    ext["kafka.version"] = '0.9.0.0'
}

allprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'groovy'
    apply plugin: 'java-library'

    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 11
    targetCompatibility = 11

    group = 'com.customproject.coffeeshop'

    configurations.implementation {
        exclude module: 'spring-boot-starter-web'
        exclude module: 'spring-boot-starter-jetty'
        exclude module: 'jetty-server'
        exclude group: 'javax.servlet'
    }

    configurations.all {
        exclude module: 'spring-boot-starter-web'
        exclude module: 'spring-boot-starter-jetty'

        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}

subprojects {
    processResources {
        String commitVersion = System.getProperty("commitVersion")
        if(commitVersion == null){
            commitVersion = ""
        }
        filesMatching(["**/*.yml", "**/*.xml"]) {
            expand version: project.version,
                    group: project.group,
                    name: project.name,
                    serviceCode: project.name.toUpperCase(),
                    appBuiltTime: new Date().getTime(),
                    commitVersion: commitVersion
        }
    }

    dependencies {
        implementation("org.projectlombok:lombok:1.18.4")
        implementation("com.fasterxml.jackson.core:jackson-core:${jacksonVersion}")
        implementation("com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}")
        implementation("com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}")
        implementation("com.fasterxml.jackson.module:jackson-module-afterburner:${jacksonVersion}")

        // spring
        implementation("org.springframework.boot:spring-boot-starter-webflux:${springBootVersion}")
        implementation("org.springframework.boot:spring-boot-configuration-processor:${springBootVersion}")

        implementation("org.springframework.boot:spring-boot-starter-reactor-netty")
        implementation("io.netty:netty-buffer")

        implementation(group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.9.+')
        implementation(group: 'io.github.microutils', name: 'kotlin-logging', version: '1.6.10')
        implementation("io.micrometer:micrometer-core:${micrometerVersion}")
        implementation("io.micrometer:micrometer-registry-prometheus:1.1.3")

        // test
        testImplementation("com.nhaarman:mockito-kotlin:1.5.0")
    }

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    task copyJars(type: Copy) {
        from "${buildDir}/libs"
        into "${buildDir}/deploy/module/${project.name}"
    }

    task printName {
        doLast {
            println "${project.name}"
        }
    }
}

 // for service projects
configure(subprojects.findAll { project.ext.serviceProjects.contains(it.name) }) {
    apply plugin: 'org.springframework.boot'

    jar {
        archiveName "service-${project.name}.jar"
    }
}

